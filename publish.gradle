/**
 * Maven Publish Helper
 *
 * Requires Android Gradle plugin 3.6.0 or higher (available since Android Studio 3.6).
 * See also: https://developer.android.com/studio/build/maven-publish-plugin
 *
 * @Author Chetan Tuteja
 * @Version 1.0
 * @Date 14 Feb 2021
 */

apply plugin: 'maven-publish'

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // Adds javadocs and sources as separate jars.
                artifact androidJavadocsJar
                artifact androidSourcesJar

                // You can customize attributes of the publication here or in module's build.gradle file.
                groupId 'com.chetantuteja.easybinding'
                artifactId 'easy-binding'
                version = android.defaultConfig.versionName

                pom.withXml {
                    final root = asNode()
                    // Attribute "scm" (and probably also "name") is required in order to be valid pom for publishing on JCenter.
                    // Bintray works without them. Maven Central requires in addition few more attributes.
                    root.appendNode('name', 'easy-binding')
                    root.appendNode('scm').appendNode('url', 'https://github.com/chetan-tuteja/easy-binding')
                }
            }
        }
    }
}

/**
 * Bintray publish configuration
 */

apply plugin: 'com.jfrog.bintray'

group "com.chetantuteja.easybinding"
version android.defaultConfig.versionName

Properties properties = new Properties()
properties.load(new FileInputStream(project.rootProject.file('local.properties')))

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ['release']
    publish = true
    override = true
    pkg {
        repo = 'maven'
        name = "easy-binding"
        userOrg = 'chetan-tuteja'
        desc = 'A Kotlin based library to reduce the Boilerplate code for ViewBinding in Android to bare necessity, to make it easy to use.'
        websiteUrl = 'https://github.com/chetan-tuteja/easy-binding'
        issueTrackerUrl = 'https://github.com/chetan-tuteja/easy-binding/issues'
        vcsUrl = 'https://github.com/chetan-tuteja/easy-binding.git'
        githubRepo = 'chetan-tuteja/easy-binding'
        licenses = ['The MIT License (MIT)']
        version {
            name = this.version
            released = new Date()
            vcsTag = 'v' + this.version
        }
    }
}